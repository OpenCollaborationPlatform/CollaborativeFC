import "property.dml" as PropertyContainer

Map {
    .name: "ObjectContainer"
    
    event onObjectCreated(string, string)           //name + typeid
    event onObjectRemoved(string)                   //name
    event onChangedObjectProperty(string, string)   //obj name, property name
    
    function NewObject(name, typeid) {
    
        var container = this
        var obj = container.New(name)
        obj.typeid = typeid
 
        obj.onChangedProperty.RegisterCallback(function(prop) {
            container.onChangedObjectProperty.Emit(name, prop)
        })
        
        container.onObjectCreated.Emit(name, typeid)
        return obj
    }
    
    function RemoveObject(name) {
        
        this.Remove(name)
        this.onObjectRemoved.Emit(name)
    }
    
    const function GetObject(name) {
        
        return this.Get(name)
    }
    
    .key: string
    .value: Data {
        
        .name: "Object"
        
        property string typeid
        
        event onChangedProperty(string)
        
        PropertyContainer {
            .name: "Properties"
            .onChangedProperty: function(name) {
                this.parent.onChangedProperty.Emit(name)
            }
        }
        
        //convinience function to add new properties
        function NewProperty(name, type, typeid, group, docu) {        
            return this.Properties.NewProperty(name, type, typeid, group, docu)
        }
        
        //convienience function to set property value
        function SetProperty(name, value) {
            var prop = this.Properties.Get(name)
            prop.SetValue(value)        
        }
        
        //convienience function to get property value
        const function GetProperty(name) {
            var prop = this.Properties.Get(name)
            return prop.GetValue()
        }
    }
}
